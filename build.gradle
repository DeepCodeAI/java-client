plugins {
    // Apply the java-library plugin to add support for Java Library
    id "java-library"
    id "com.github.johnrengelman.shadow" version "5.2.0"
    id("maven-publish")
    id "com.jfrog.bintray" version "1.8.5"
}

repositories {
    jcenter()
}

group = "io.snyk.code.sdk"
archivesBaseName = "snyk-code-client"
version = "2.1.4"

sourceCompatibility = 11

java {
    withJavadocJar()
    withSourcesJar()
}

sourceSets {
    integTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    integTestImplementation.extendsFrom implementation
    integTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    implementation "com.squareup.retrofit2:retrofit:2.7.1"
    implementation "com.squareup.retrofit2:converter-gson:2.7.1"
    implementation "org.jetbrains:annotations:15.0"

    testImplementation "junit:junit:4.12"
    integTestImplementation "junit:junit:4.12"
}

task integTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integTest.output.classesDirs
    classpath = sourceSets.integTest.runtimeClasspath
    shouldRunAfter test

    testLogging {
        showStandardStreams = true
    }
    outputs.upToDateWhen { false }
}
integTest.dependsOn test
